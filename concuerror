#!/usr/bin/env escript
%% -*- erlang-indent-level: 2 -*-
%%! +S1 -noshell -pa . -pa ebin

main(Args) ->
  ScriptDir = filename:dirname(script_name()),
  EbinDir = filename:join([ScriptDir,"ebin"]),
  GetoptDir = filename:join([ScriptDir,"deps","getopt","ebin"]),
  ok = code:add_pathsa([EbinDir, GetoptDir]),
  Cover = os:getenv("CONCUERROR_COVER"),
  if Cover =/= false ->
      cover:start(),
      cover:compile_beam_directory(EbinDir);
     true -> ok
  end,
  Status =
    case concuerror_options:parse_cl(Args) of
      {ok, Options} -> concuerror:run(Options);
      {exit, ExitStatus} -> ExitStatus
    end,
  if Cover =/= false ->
      Hash = binary:decode_unsigned(erlang:md5(lists:flatten(Args))),
      Out = filename:join([Cover,io_lib:format("~.16b",[Hash])]),
      cover:export(Out);
     true -> ok
  end,
  cl_exit(Status).

-spec script_name() -> file:filename().
script_name() ->
  Scriptname = escript:script_name(),
  case file:read_link_all(Scriptname) of
    {ok, Filename} -> Filename;
    _Other -> Scriptname
  end.

-spec cl_exit(concuerror:exit_status()) -> no_return().

cl_exit(ok) ->
  erlang:halt(0);
cl_exit(error) ->
  erlang:halt(1);
cl_exit(fail) ->
  erlang:halt(2).
